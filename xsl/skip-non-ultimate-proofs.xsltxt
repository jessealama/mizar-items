// skip-non-ultimate-proofs.xsl : Remove proofs from all elements except the final one

stylesheet "1.0";

output method="xml" indent="no";

tpl [*] mode="copy" {
  $n = `local-name (.)`;
  <$n {
    for-each [@*] { copy-of `.`; }
    if [*] {
      "\n"; apply [*] mode="copy";
    }
  }
  "\n";
}

tpl [*] mode="skip-proofs" {
  $n = `local-name (.)`;
  <$n {
    for-each [@*] { copy-of `.`; }
    if [*] {
      "\n"; apply [*] mode="skip-proofs";
    }
  }
  "\n";
}

tpl [Now[@nr and @line and @col]] mode="skip-proofs" {
  <Proposition {
    @line = `@line`;
    @col = `@col`;
    @nr = `@nr`;
    if [@vid] { @vid = `@vid`; }
    "\n";
    for-each [BlockThesis] {
      apply [*[position() = last()]] mode="copy";
    }
  }
  "\n"; "\n";
  <SkippedProof; "\n";
}

tpl [Proof] mode="skip-proofs" {
  <SkippedProof; "\n";
}

tpl [By] mode="skip-proofs" {
  <SkippedProof; "\n";
}

tpl [From] mode="skip-proofs" {
  <SkippedProof; "\n";
}

tpl [/] {
  if [not(Article)] {
    msg terminate="yes" "Error: the required Article document element is missing.";
  }
  apply [Article];
}

tpl [Article[not(@aid)]] {
  msg terminate="yes" "Error: the Article element must have an aid attribute.";
}

tpl [Article[@aid]] {
  <Article {
    @aid = `@aid`;
    "\n";
    for-each [*] {
      if [position() = last()] {
        apply [.] mode="copy"; "\n";
      } else {
        apply [.] mode="skip-proofs"; "\n";
      }
    }
  }
}