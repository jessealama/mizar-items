environ
vocabularies FUNCT_1,XBOOLE_0,RELAT_1,FUNCT_4,SUBSET_1,NUMBERS,ABIAN,FUNCT_7,ARYTM_3,COHSP_1,TARSKI,FUNCOP_1,ZFMISC_1,SETFAM_1,FUNCT_2,LATTICES,STRUCT_0,ORDINAL1,ORDINAL2,EQREL_1,CARD_1,BINOP_1,LATTICE3,PBOOLE,FILTER_0,FILTER_1,WELLORD1,RELAT_2,ORDERS_1,ORDERS_2,XXREAL_0,REWRITE1,XXREAL_2,SEQM_3,KNASTER;
notations TARSKI,XBOOLE_0,ZFMISC_1,SUBSET_1,CARD_1,NUMBERS,NAT_1,RELAT_1,RELAT_2,ORDERS_1,STRUCT_0,FUNCT_1,RELSET_1,PARTFUN1,FUNCT_2,BINOP_1,FUNCOP_1,SETFAM_1,FUNCT_4,WELLORD1,ORDINAL1,ORDINAL2,COHSP_1,LATTICES,LATTICE3,QUANTAL1,ORDERS_2,ABIAN,CKB4,CKB6,CKB7;
definitions TARSKI,RELAT_1,FUNCT_1,COHSP_1,LATTICE3,QUANTAL1,XBOOLE_0,ABIAN,WELLORD1,CKB4,CKB6,CKB7;
theorems TARSKI,SETFAM_1,ZFMISC_1,RELAT_1,FUNCT_1,FUNCOP_1,FUNCT_2,FUNCT_4,CARD_1,COHSP_1,LATTICES,LATTICE3,QUANTAL1,FILTER_0,FILTER_1,BOOLEALG,WELLORD1,RELSET_1,ORDERS_2,RELAT_2,ORDINAL1,CARD_3,XBOOLE_0,XBOOLE_1,FUNCT_7,PARTFUN1,ORDERS_1,ABIAN,NAT_1,CKB1,CKB2,CKB3,CKB4,CKB6,CKB7;
schemes NAT_1,FUNCT_1,FUNCT_2,DOMAIN_1,ORDINAL2;
registrations XBOOLE_0,SUBSET_1,FUNCT_1,ORDINAL1,RELSET_1,CARD_1,STRUCT_0,LATTICES,LATTICE3,QUANTAL1,CKB5;
constructors SETFAM_1,WELLORD1,WELLORD2,BINOP_1,DOMAIN_1,ORDINAL2,NAT_1,ABIAN,BOOLEALG,QUANTAL1,COHSP_1,RELSET_1,CKB4,CKB6,CKB7;
requirements NUMERALS,BOOLE,SUBSET;
begin
reserve X for set;
reserve f for  c=-monotone (Function of ( bool X ),( bool X ));
theorem
Th6: ( lfp (X,f) ) is_a_fixpoint_of f
proof
defpred P[ set ]
 means
( f . $1 ) c= $1;
reconsider H = { h where h is (Subset of X): P[ h ] } as (Subset-Family of X) from DOMAIN_1:sch 7;
reconsider H as (Subset-Family of X);
set A = ( meet H );
now
X c= X;
then reconsider X9 = X as (Subset of X);
( f . X9 ) c= X9;
then X9 in H;
hence H <> ( {} );
let h being set;
assume A1: h in H;
then consider x being (Subset of X) such that A2: x = h and A3: ( f . x ) c= x;
A c= h by A1,SETFAM_1:3;
then ( f . A ) c= ( f . x ) by A2,CKB4:def 1;
hence ( f . A ) c= h by A2,A3,XBOOLE_1:1;
end;
then A4: ( f . A ) c= A by SETFAM_1:5;
then ( f . ( f . A ) ) c= ( f . A ) by CKB4:def 1;
then ( f . A ) in H;
then A c= ( f . A ) by SETFAM_1:3;
hence ( f . ( lfp (X,f) ) ) = ( lfp (X,f) ) by A4,XBOOLE_0:def 10;
end;
