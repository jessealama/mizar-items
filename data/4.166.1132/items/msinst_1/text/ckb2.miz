environ
vocabularies XBOOLE_0,STRUCT_0,MSUALG_1,ALTCAT_1,MSALIMIT,SUBSET_1,FUNCT_1,CAT_1,RELAT_1,PBOOLE,ZFMISC_1,MCART_1,PUA2MSS1,RELAT_2,BINOP_1,TARSKI,MSUALG_6,FUNCT_2,PARTFUN1,NUMBERS,CARD_3,FUNCOP_1,PZFMISC1,MARGREL1,MEMBER_1,FUNCT_6,FINSEQ_4,MSUALG_3,MSINST_1;
notations TARSKI,XBOOLE_0,ZFMISC_1,SUBSET_1,NUMBERS,MCART_1,RELAT_1,FUNCT_1,STRUCT_0,FUNCT_2,MCART_2,RELSET_1,PARTFUN1,FINSEQ_2,CARD_3,BINOP_1,MULTOP_1,FUNCT_6,FUNCOP_1,PBOOLE,PZFMISC1,PRALG_1,MSUALG_1,MSUALG_3,ALTCAT_1,PRALG_2,PUA2MSS1,MSUALG_6,MSALIMIT,CKB1;
definitions ALTCAT_1,MSUALG_3,PBOOLE,TARSKI,XBOOLE_0,BINOP_1,CKB1;
theorems XBOOLE_0,ALTCAT_1,ALTCAT_2,AUTALG_1,CARD_3,CLOSURE1,FUNCOP_1,FUNCT_1,FUNCT_2,FUNCT_6,FUNCTOR0,MCART_1,MCART_2,MSSUBFAM,MSUALG_1,MSUALG_3,MSUALG_6,MSALIMIT,MULTOP_1,PARTFUN1,PBOOLE,PRALG_2,PUA2MSS1,RELAT_1,TARSKI,ZFMISC_1,PRALG_1,XBOOLE_1,PZFMISC1,FINSEQ_2,CKB1;
schemes ALTCAT_1,MSSUBFAM,TARSKI;
registrations XBOOLE_0,SUBSET_1,FUNCT_1,RELSET_1,FUNCOP_1,RELAT_1,STRUCT_0,MSUALG_1,ALTCAT_1,MSUALG_6,MSALIMIT,MSSUBFAM,PBOOLE;
constructors MCART_2,PZFMISC1,MSUALG_3,ALTCAT_1,PUA2MSS1,CLOSURE1,MSUALG_6,MSALIMIT,RELSET_1,CKB1;
requirements SUBSET,BOOLE;
begin
reserve A for non empty set;
registration
let A;
cluster ( MSSCat A ) ->  transitive  associative  with_units;
coherence
proof
set M = ( MSSCat A );
set G = ( MSSCat A );
thus G is  transitive
proof
let o1 being (object of G),o2 being (object of G),o3 being (object of G);
reconsider o19 = o1,o29 = o2,o39 = o3 as (Element of ( MSS_set A )) by CKB1:def 1;
assume that
A1: <^ o1,o2 ^> <> ( {} )
and
A2: <^ o2,o3 ^> <> ( {} );
set s = the (Element of ( MSS_morph (o29,o39) ));
( MSS_morph (o29,o39) ) <> ( {} ) by A2,CKB1:def 1;
then consider u being Function,w being Function such that s = [ u,w ] and A3: u,w form_morphism_between o29,o39 by MSALIMIT:def 10;
set t = the (Element of ( MSS_morph (o19,o29) ));
( MSS_morph (o19,o29) ) <> ( {} ) by A1,CKB1:def 1;
then consider f being Function,g being Function such that t = [ f,g ] and A4: f,g form_morphism_between o19,o29 by MSALIMIT:def 10;
( u * f ),( w * g ) form_morphism_between o19,o39 by A4,A3,PUA2MSS1:29;
then [ ( u * f ),( w * g ) ] in ( MSS_morph (o19,o39) ) by MSALIMIT:def 10;
hence thesis by CKB1:def 1;
end;

set G = (the Arrows of M);
set C = (the Comp of M);
thus C is  associative
proof
let i being (Element of M),j being (Element of M),k being (Element of M),l being (Element of M);
reconsider I = i,J = j,K = k,L = l as (object of M);
let f being set,g being set,h being set;
reconsider i9 = i,j9 = j,k9 = k,l9 = l as (Element of ( MSS_set A )) by CKB1:def 1;
assume that
A5: f in ( G . (i,j) )
and
A6: g in ( G . (j,k) )
and
A7: h in ( G . (k,l) );
g in ( MSS_morph (j9,k9) ) by A6,CKB1:def 1;
then consider g1 being Function,g2 being Function such that A8: g = [ g1,g2 ] and A9: g1,g2 form_morphism_between j9,k9 by MSALIMIT:def 10;
f in ( MSS_morph (i9,j9) ) by A5,CKB1:def 1;
then consider f1 being Function,f2 being Function such that A10: f = [ f1,f2 ] and A11: f1,f2 form_morphism_between i9,j9 by MSALIMIT:def 10;
A12: ( ( C . (i,j,k) ) . (g,f) ) = [ ( g1 * f1 ),( g2 * f2 ) ] by A5,A6,A10,A11,A8,A9,CKB1:def 1;
h in ( MSS_morph (k9,l9) ) by A7,CKB1:def 1;
then consider h1 being Function,h2 being Function such that A13: h = [ h1,h2 ] and A14: h1,h2 form_morphism_between k9,l9 by MSALIMIT:def 10;
A15: ( ( C . (j,k,l) ) . (h,g) ) = [ ( h1 * g1 ),( h2 * g2 ) ] by A6,A7,A8,A9,A13,A14,CKB1:def 1;
( h1 * g1 ),( h2 * g2 ) form_morphism_between j9,l9 by A9,A14,PUA2MSS1:29;
then [ ( h1 * g1 ),( h2 * g2 ) ] in ( MSS_morph (j9,l9) ) by MSALIMIT:def 10;
then A16: [ ( h1 * g1 ),( h2 * g2 ) ] in ( G . (j,l) ) by CKB1:def 1;
A17: (( ( h1 * g1 ) * f1 ) = ( h1 * ( g1 * f1 ) ) & ( ( h2 * g2 ) * f2 ) = ( h2 * ( g2 * f2 ) )) by RELAT_1:36;
J in (the carrier of M);
then A18: J in ( MSS_set A ) by CKB1:def 1;
L in (the carrier of M);
then A19: L in ( MSS_set A ) by CKB1:def 1;
( g1 * f1 ),( g2 * f2 ) form_morphism_between i9,k9 by A11,A9,PUA2MSS1:29;
then [ ( g1 * f1 ),( g2 * f2 ) ] in ( MSS_morph (i9,k9) ) by MSALIMIT:def 10;
then A20: [ ( g1 * f1 ),( g2 * f2 ) ] in ( G . (i,k) ) by CKB1:def 1;
I in (the carrier of M);
then A21: I in ( MSS_set A ) by CKB1:def 1;
K in (the carrier of M);
then K in ( MSS_set A ) by CKB1:def 1;
then ( ( C . (i,k,l) ) . (h,[ ( g1 * f1 ),( g2 * f2 ) ]) ) = [ ( h1 * ( g1 * f1 ) ),( h2 * ( g2 * f2 ) ) ] by A21,A19,A7,A13,A20,CKB1:def 1;
hence thesis by A21,A18,A19,A5,A10,A12,A15,A16,A17,CKB1:def 1;
end;

thus C is  with_left_units
proof
let j being (Element of M);
reconsider j9 = j as (Element of ( MSS_set A )) by CKB1:def 1;
set e1 = ( id (the carrier of j9) );
set e2 = ( id (the carrier' of j9) );
take e = [ e1,e2 ];
(e1,e2 form_morphism_between j9,j9 & ( G . (j,j) ) = ( MSS_morph (j9,j9) )) by CKB1:def 1,PUA2MSS1:28;
hence A22: e in ( G . (j,j) ) by MSALIMIT:def 10;
let i being (Element of M);
reconsider i9 = i as (Element of ( MSS_set A )) by CKB1:def 1;
let f being set;
reconsider I = i,J = j as (object of M);
assume A23: f in ( G . (i,j) );
then f in ( MSS_morph (i9,j9) ) by CKB1:def 1;
then consider f1 being Function,f2 being Function such that A24: f = [ f1,f2 ] and A25: f1,f2 form_morphism_between i9,j9 by MSALIMIT:def 10;
A26: ( rng f2 ) c= (the carrier' of j9) by A25,PUA2MSS1:def 12;
( rng f1 ) c= (the carrier of j9) by A25,PUA2MSS1:def 12;
then A27: ( e1 * f1 ) = f1 by RELAT_1:53;
( ( C . (I,J,J) ) . ([ e1,e2 ],[ f1,f2 ]) ) = [ ( e1 * f1 ),( e2 * f2 ) ] by A22,A23,A24,A25,CKB1:def 1;
hence thesis by A24,A26,A27,RELAT_1:53;
end;

thus C is  with_right_units
proof
let i being (Element of M);
reconsider i9 = i as (Element of ( MSS_set A )) by CKB1:def 1;
set e1 = ( id (the carrier of i9) );
set e2 = ( id (the carrier' of i9) );
take e = [ e1,e2 ];
(e1,e2 form_morphism_between i9,i9 & ( G . (i,i) ) = ( MSS_morph (i9,i9) )) by CKB1:def 1,PUA2MSS1:28;
hence A28: e in ( G . (i,i) ) by MSALIMIT:def 10;
let j being (Element of M);
reconsider j9 = j as (Element of ( MSS_set A )) by CKB1:def 1;
let f being set;
reconsider I = i,J = j as (object of M);
assume A29: f in ( G . (i,j) );
then f in ( MSS_morph (i9,j9) ) by CKB1:def 1;
then consider f1 being Function,f2 being Function such that A30: f = [ f1,f2 ] and A31: f1,f2 form_morphism_between i9,j9 by MSALIMIT:def 10;
A32: ( dom f2 ) = (the carrier' of i9) by A31,PUA2MSS1:def 12;
( dom f1 ) = (the carrier of i9) by A31,PUA2MSS1:def 12;
then A33: ( f1 * e1 ) = f1 by RELAT_1:52;
( ( C . (I,I,J) ) . ([ f1,f2 ],[ e1,e2 ]) ) = [ ( f1 * e1 ),( f2 * e2 ) ] by A28,A29,A30,A31,CKB1:def 1;
hence thesis by A30,A32,A33,RELAT_1:52;
end;

end;
end;
