environ
vocabularies ABIAN,AOFA_I00,ARYTM_1,ARYTM_3,CARD_1,CARD_3,CLASSES1,COMPLEX1,CONVEX1,EUCLID,EUCLID_9,FINSEQ_1,FINSEQ_2,FINSEQ_4,FINSET_1,FUNCOP_1,FUNCT_1,FUNCT_2,FUNCT_4,GLIB_000,INT_1,JORDAN2C,MATROID0,MEASURE5,MEMBERED,METRIC_1,NAT_1,NEWTON,NUMBERS,ORDERS_1,ORDINAL1,ORDINAL2,ORDINAL4,PARTFUN1,PCOMPS_1,PEPIN,POWER,PRE_TOPC,QC_LANG1,RCOMP_1,REAL_1,RELAT_1,RLAFFIN1,RLAFFIN2,RLVECT_1,RLVECT_2,RLVECT_5,RUSUB_4,SEMI_AF1,SEQ_4,SETFAM_1,SGRAPH1,SIMPLEX0,SIMPLEX1,SQUARE_1,STRUCT_0,SUBSET_1,SUPINF_2,TARSKI,TOPDIM_1,TOPMETR,TOPS_1,TOPS_2,VALUED_1,WEIERSTR,XBOOLE_0,XREAL_0,XXREAL_0,XXREAL_1,XXREAL_2,ZFMISC_1,SIMPLEX2;
notations TARSKI,XBOOLE_0,SUBSET_1,ORDINAL1,ZFMISC_1,RELAT_1,FUNCT_1,RELSET_1,FUNCT_2,PARTFUN1,FUNCOP_1,FUNCT_4,XXREAL_3,VALUED_1,NAT_1,PRE_TOPC,NUMBERS,XREAL_0,FINSEQ_1,BINOP_1,FINSEQ_2,TOPMETR,METRIC_1,EUCLID,FINSET_1,XXREAL_0,REAL_1,SETFAM_1,TOPS_2,INT_1,STRUCT_0,COMPTS_1,PCOMPS_1,RCOMP_1,WEIERSTR,SEQ_4,XXREAL_2,RVSUM_1,RLVECT_1,RUSUB_4,RLVECT_5,SIMPLEX0,RLAFFIN1,RLAFFIN2,SIMPLEX1,ORDERS_1,CARD_1,DOMAIN_1,PENCIL_1,CLASSES1,MATROID0,TBSP_1,MEMBERED,RLVECT_2,CONVEX1,TOPREAL9,COMPLEX1,NEWTON,POWER,JORDAN2C,SQUARE_1,BORSUK_1,CARD_3,EUCLID_9,TMAP_1,FINSEQOP,ABIAN,WAYBEL23,TOPDIM_1,TOPDIM_2,RLAFFIN3;
definitions EUCLID,METRIC_1,ORDINAL1,PCOMPS_1,RLVECT_1,SIMPLEX0,SIMPLEX1,STRUCT_0,SUBSET_1,TARSKI,TBSP_1,XCMPLX_0,CARD_1;
theorems ABIAN,ABSVALUE,BINOP_1,BORSUK_1,BORSUK_3,BORSUK_5,CARD_1,CARD_2,CLASSES1,COMPTS_1,CONVEX1,EUCLID,EUCLID_9,FINSEQ_1,FINSEQ_2,FINSEQ_3,FINSEQ_4,FUNCOP_1,FUNCT_1,FUNCT_2,FUNCT_4,HAUSDORF,INT_1,JORDAN1K,JORDAN2B,JORDAN2C,MATROID0,MEMBERED,METRIC_1,NAT_1,NEWTON,ORDERS_1,ORDINAL1,PARTFUN1,PCOMPS_1,PCOMPS_2,POWER,PRE_TOPC,RCOMP_1,RELAT_1,RLAFFIN1,RLAFFIN2,RLAFFIN3,RLVECT_1,RLVECT_2,RLVECT_3,RUSUB_4,RVSUM_1,SETFAM_1,SIMPLEX0,SIMPLEX1,TARSKI,TBSP_1,TMAP_1,TOPDIM_1,TOPDIM_2,TOPGEN_5,TOPGRP_1,TOPMETR,TOPREAL3,TOPREAL6,TOPREAL7,TOPREAL9,TOPREALC,TOPS_1,TOPS_2,TOPS_3,TSEP_1,VALUED_1,WEIERSTR,XBOOLE_0,XBOOLE_1,XCMPLX_1,XREAL_0,XREAL_1,XXREAL_0,XXREAL_1,XXREAL_2,XXREAL_3,ZFMISC_1;
schemes FINSEQ_1,FRAENKEL,FUNCT_2,NAT_1,NAT_2,SUBSET_1;
registrations ASYMPT_0,BORSUK_1,BORSUK_2,CARD_1,COMPTS_1,DILWORTH,EUCLID,EUCLID_9,FINSEQ_1,FINSEQ_2,FINSET_1,FUNCT_1,FUNCT_2,INT_1,JORDAN2B,JORDAN2C,MEMBERED,METRIZTS,MONOID_0,NAT_1,NEWTON,NUMBERS,PCOMPS_1,PRE_TOPC,RELAT_1,RELSET_1,RLAFFIN1,RLAFFIN3,SEQ_4,SIMPLEX0,SIMPLEX1,SPRECT_1,STRUCT_0,SUBSET_1,TBSP_1,TMAP_1,TOPDIM_1,TOPDIM_2,TOPMETR,TOPREAL9,TOPREALC,TOPS_1,VALUED_0,VALUED_1,XBOOLE_0,XREAL_0,XXREAL_0,XXREAL_2,XXREAL_3,YELLOW13,JORDAN;
constructors ABIAN,BINOP_2,COMPTS_1,CONVEX1,EUCLID_9,FINSEQOP,FUNCSDOM,JORDAN2C,LEXBFS,MONOID_0,NEWTON,POWER,REAL_1,RFINSEQ2,RLAFFIN1,RLAFFIN2,RLAFFIN3,RLVECT_5,RUSUB_5,SEQ_1,SEQ_4,SIMPLEX1,SQUARE_1,TBSP_1,TMAP_1,TOPDIM_1,TOPDIM_2,TOPREAL9,TOPS_2,WAYBEL23,WEIERSTR;
requirements REAL,SUBSET,NUMERALS,ARITHM,BOOLE;
begin
reserve M for non empty MetrSpace;
definition
let M;
let F being (Subset-Family of ( TopSpaceMetr M ));
assume that
A1: ( TopSpaceMetr M ) is  compact;
assume that
A2: F is  open
and
A3: F is (Cover of ( TopSpaceMetr M ));
mode Lebesgue_number of F
 ->  positive Real
means :Def1: (for p being (Point of M) holds (ex A being (Subset of ( TopSpaceMetr M )) st (A in F & ( Ball (p,it) ) c= A)));
existence
proof
set TM = ( TopSpaceMetr M );
consider G being (Subset-Family of TM) such that A4: G c= F and A5: G is (Cover of TM) and A6: G is  finite by A1,A2,A3,COMPTS_1:def 1;
per cases ;
suppose A7: ( [#] TM ) in G;

take R = 1;
let x being (Point of M);
take ( [#] TM );
thus thesis by A4,A7;
end;
suppose A8: (not ( [#] TM ) in G);

set cTM = ( [#] TM );
set FUNCS = ( Funcs (( [#] TM ),( REAL )) );
consider g being FinSequence such that A9: ( rng g ) = G and g is  one-to-one by A6,FINSEQ_4:58;
defpred P[ Nat,set,set ]
 means
(for f1,f2 being (Function of TM,( R^1 )) holds (((f1 = $2 & f2 = $3) & f1 is  continuous) implies (f2 is  continuous & (ex A being non empty (Subset of TM) st (( A ` ) = ( g . ( $1 + 1 ) ) & (for x being (Point of TM) holds ( f2 . x ) = ( max (( f1 . x ),( ( dist_min A ) . x )) )))))));
( union G ) is non empty by A5,SETFAM_1:45;
then A10: g is non empty by A9,ZFMISC_1:2;
then A11: ( len g ) >= 1 by NAT_1:14;
then A12: 1 in ( dom g ) by FINSEQ_3:25;
then A13: ( g . 1 ) in ( rng g ) by FUNCT_1:def 3;
then reconsider g1 = ( g . 1 ) as (Subset of TM) by A9;
A14: ( ( g1 ` ) ` ) <> ( [#] TM ) by A8,A9,A12,FUNCT_1:def 3;
g1 is  open by A2,A4,A9,A13,TOPS_2:def 1;
then reconsider g19 = ( g1 ` ) as non empty  closed (Subset of TM) by A14;
reconsider Dg19 = ( dist_min g19 ) as (Element of FUNCS) by FUNCT_2:8,TOPMETR:17;
A15: (for n being Nat holds ((1 <= n & n < ( len g )) implies (for x being (Element of FUNCS) holds (ex y being (Element of FUNCS) st P[ n,x,y ]))))
proof
let n being Nat;
assume that
1 <= n
and
A16: n < ( len g );
let x being (Element of FUNCS);
reconsider fx = x as (Function of TM,( R^1 )) by TOPMETR:17;
per cases ;
suppose A17: (not fx is  continuous);

take y = x;
thus thesis by A17;
end;
suppose A18: fx is  continuous;

(1 <= ( n + 1 ) & ( n + 1 ) <= ( len g )) by A16,NAT_1:11,NAT_1:13;
then A19: ( n + 1 ) in ( dom g ) by FINSEQ_3:25;
then ( g . ( n + 1 ) ) in G by A9,FUNCT_1:def 3;
then reconsider A = ( g . ( n + 1 ) ) as (Subset of TM);
( ( A ` ) ` ) <> ( [#] TM ) by A8,A9,A19,FUNCT_1:def 3;
then reconsider A9 = ( A ` ) as non empty (Subset of TM);
( R^1 ) is (SubSpace of ( R^1 )) by TSEP_1:2;
then consider h being  continuous (Function of TM,( R^1 )) such that A20: (for x being (Point of TM) holds ( h . x ) = ( max (( fx . x ),( ( dist_min A9 ) . x )) )) by A18,TOPGEN_5:50;
reconsider y = h as (Element of FUNCS) by FUNCT_2:8,TOPMETR:17;
take y;
let f1 being (Function of TM,( R^1 )),f2 being (Function of TM,( R^1 ));
assume that
A21: f1 = x
and
A22: f2 = y
and
f1 is  continuous;
thus f2 is  continuous by A22;
take A9;
thus thesis by A20,A21,A22;
end;
end;
consider p being (FinSequence of FUNCS) such that A23: ( len p ) = ( len g ) and A24: (( p /. 1 ) = Dg19 or ( len g ) = ( 0 )) and A25: (for n being Nat holds ((1 <= n & n < ( len g )) implies P[ n,( p /. n ),( p /. ( n + 1 ) ) ])) from NAT_2:sch 1(A15);
A26: ( len p ) in ( dom p ) by A11,A23,FINSEQ_3:25;
( p /. ( len p ) ) is (Element of FUNCS);
then reconsider pL = ( p /. ( len p ) ) as (Function of TM,( R^1 )) by TOPMETR:17;
reconsider rngPL = ( rng pL ) as (Subset of ( R^1 )) by RELAT_1:def 19;
set cRpL = ( [#] rngPL );
A27: cRpL = ( rng pL ) by WEIERSTR:def 1;
A28: ( dom p ) = ( dom g ) by A23,FINSEQ_3:29;
defpred Q[ Nat ]
 means
(for f being (Function of TM,( R^1 )) holds (($1 in ( dom p ) & f = ( p /. $1 )) implies (f is  continuous & (for j being Nat holds (for h being (Function of TM,( R^1 )) holds (((j <= $1 & j in ( dom p )) & h = ( p /. j )) implies (for x being (Point of TM) holds ( h . x ) <= ( f . x ))))))));
A29: (for n being Nat holds (Q[ n ] implies Q[ ( n + 1 ) ]))
proof
let n being Nat;
assume A30: Q[ n ];
let f being (Function of TM,( R^1 ));
assume that
A31: ( n + 1 ) in ( dom p )
and
A32: f = ( p /. ( n + 1 ) );
per cases ;
suppose A33: n = ( 0 );

hence f is  continuous by A10,A24,A32;
let j being Nat;
let g being (Function of TM,( R^1 ));
assume that
A34: j <= ( n + 1 )
and
A35: j in ( dom p )
and
A36: g = ( p /. j );
1 <= j by A35,FINSEQ_3:25;
hence thesis by A32,A33,A34,A36,XXREAL_0:1;
end;
suppose n > ( 0 );

then reconsider n1 = ( n - 1 ) as (Element of ( NAT )) by NAT_1:20;
( p /. n ) is (Element of FUNCS);
then reconsider pn = ( p /. n ) as (Function of TM,( R^1 )) by TOPMETR:17;
( n + 1 ) <= ( len p ) by A31,FINSEQ_3:25;
then A37: (1 <= ( n1 + 1 ) & n < ( len p )) by NAT_1:11,NAT_1:13;
then A38: n in ( dom p ) by FINSEQ_3:25;
then A39: pn is  continuous by A30;
hence f is  continuous by A23,A25,A32,A37;
consider A being non empty (Subset of TM) such that ( A ` ) = ( g . ( n + 1 ) ) and A40: (for y being (Point of TM) holds ( f . y ) = ( max (( pn . y ),( ( dist_min A ) . y )) )) by A23,A25,A32,A37,A39;
let j being Nat;
let h being (Function of TM,( R^1 ));
assume that
A41: j <= ( n + 1 )
and
A42: j in ( dom p )
and
A43: h = ( p /. j );
let x being (Point of TM);
A44: ( f . x ) = ( max (( pn . x ),( ( dist_min A ) . x )) ) by A40;
per cases  by A41,NAT_1:8;
suppose A45: j <= n;

A46: ( pn . x ) <= ( f . x ) by A44,XXREAL_0:25;
( h . x ) <= ( pn . x ) by A30,A38,A42,A43,A45;
hence thesis by A46,XXREAL_0:2;
end;
suppose j = ( n + 1 );

hence thesis by A32,A43;
end;
end;
end;
A47: Q[ ( 0 ) ] by FINSEQ_3:25;
A48: (for n being Nat holds Q[ n ]) from NAT_1:sch 2(A47,A29);
A49: ( dom pL ) = cTM by FUNCT_2:def 1;
then ( pL .: cTM ) = ( rng pL ) by RELAT_1:113;
then A50: rngPL is  compact by A1,A26,A48,WEIERSTR:9;
then A51: cRpL is  compact by WEIERSTR:13;
reconsider cRpL as non empty  bounded (Subset of ( REAL )) by A50,WEIERSTR:11,WEIERSTR:def 1;
set L = ( lower_bound cRpL );
L in cRpL by A51,RCOMP_1:14;
then consider x being set such that A52: x in ( dom pL ) and A53: ( pL . x ) = L by A27,FUNCT_1:def 3;
( union G ) = ( [#] TM ) by A5,SETFAM_1:45;
then consider Y being set such that A54: x in Y and A55: Y in ( rng g ) by A9,A52,TARSKI:def 4;
reconsider x as (Point of TM) by A52;
consider j being set such that A56: j in ( dom g ) and A57: ( g . j ) = Y by A55,FUNCT_1:def 3;
reconsider j as Nat by A56;
A58: j <= ( len g ) by A56,FINSEQ_3:25;
A59: 1 <= j by A56,FINSEQ_3:25;
now
per cases  by A59,XXREAL_0:1;
suppose A60: j = 1;

then (not x in g19) by A54,A57,XBOOLE_0:def 5;
then ( ( dist_min g19 ) . x ) <> ( 0 ) by HAUSDORF:9;
then ( ( dist_min g19 ) . x ) > ( 0 ) by JORDAN1K:9;
hence ( 0 ) < L by A23,A24,A26,A28,A48,A53,A56,A58,A60;
end;
suppose A61: j > 1;

then reconsider j1 = ( j - 1 ) as (Element of ( NAT )) by NAT_1:20;
(( p /. j1 ) is (Element of FUNCS) & ( p /. j ) is (Element of FUNCS));
then reconsider pj1 = ( p /. j1 ),pj = ( p /. j ) as (Function of TM,( R^1 )) by TOPMETR:17;
( j1 + 1 ) > 1 by A61;
then A62: (1 <= j1 & j1 < ( len g )) by A58,NAT_1:13;
then j1 in ( dom p ) by A23,FINSEQ_3:25;
then A63: pj1 is  continuous by A48;
P[ j1,pj1,pj ] by A25,A28,A48,A56,A62;
then consider A being non empty (Subset of TM) such that A64: ( A ` ) = ( g . j ) and A65: (for x being (Point of TM) holds ( pj . x ) = ( max (( pj1 . x ),( ( dist_min A ) . x )) )) by A63;
( A ` ) is  open by A2,A4,A9,A55,A57,A64,TOPS_2:def 1;
then A66: A is  closed by TOPS_1:3;
(not x in A) by A54,A57,A64,XBOOLE_0:def 5;
then ( ( dist_min A ) . x ) <> ( 0 ) by A66,HAUSDORF:9;
then A67: ( ( dist_min A ) . x ) > ( 0 ) by JORDAN1K:9;
( pj . x ) = ( max (( pj1 . x ),( ( dist_min A ) . x )) ) by A65;
then ( pj . x ) > ( 0 ) by A67,XXREAL_0:25;
hence ( 0 ) < L by A23,A26,A28,A48,A53,A56,A58;
end;
end;
then reconsider L as  positive Real;
take L;
let X being (Point of M);
defpred P[ Nat ]
 means
($1 in ( dom p ) & (for f1 being (Function of TM,( R^1 )) holds (( p /. $1 ) = f1 implies ( f1 . X ) >= L)));
( pL . X ) in cRpL by A27,A49,FUNCT_1:def 3;
then P[ ( len p ) ] by A11,A23,FINSEQ_3:25,XXREAL_2:3;
then A68: (ex k being Nat st P[ k ]);
consider k being Nat such that A69: P[ k ] and A70: (for n being Nat holds (P[ n ] implies k <= n)) from NAT_1:sch 5(A68);
A71: 1 <= k by A69,FINSEQ_3:25;
A72: k <= ( len p ) by A69,FINSEQ_3:25;
per cases  by A71,XXREAL_0:1;
suppose A73: k = 1;

take g1;
thus g1 in F by A4,A9,A13;
let y being set;
assume that
A74: y in ( Ball (X,L) );
reconsider Y = y as (Point of M) by A74;
A75: ( dist (X,Y) ) < L by A74,METRIC_1:11;
assume (not y in g1);
then Y in g19 by XBOOLE_0:def 5;
then A76: ( ( dist_min g19 ) . X ) <= ( dist (X,Y) ) by HAUSDORF:13;
( ( dist_min g19 ) . X ) >= L by A10,A24,A69,A73;
hence contradiction by A75,A76,XXREAL_0:2;
end;
suppose A77: k > 1;

then reconsider k1 = ( k - 1 ) as (Element of ( NAT )) by NAT_1:20;
(( p /. k1 ) is (Element of FUNCS) & ( p /. k ) is (Element of FUNCS));
then reconsider pk1 = ( p /. k1 ),pk = ( p /. k ) as (Function of TM,( R^1 )) by TOPMETR:17;
( k1 + 1 ) > 1 by A77;
then A78: (1 <= k1 & k1 < ( len g )) by A23,A72,NAT_1:13;
then k1 in ( dom p ) by A23,FINSEQ_3:25;
then A79: pk1 is  continuous by A48;
P[ k1,pk1,pk ] by A25,A48,A69,A78;
then consider A being non empty (Subset of TM) such that A80: ( A ` ) = ( g . k ) and A81: (for x being (Point of TM) holds ( pk . x ) = ( max (( pk1 . x ),( ( dist_min A ) . x )) )) by A79;
take ( A ` );
( A ` ) in G by A9,A28,A69,A80,FUNCT_1:def 3;
hence ( A ` ) in F by A4;
let y being set;
assume that
A82: y in ( Ball (X,L) );
reconsider Y = y as (Point of M) by A82;
assume (not y in ( A ` ));
then Y in A by XBOOLE_0:def 5;
then A83: ( ( dist_min A ) . X ) <= ( dist (X,Y) ) by HAUSDORF:13;
( dist (X,Y) ) < L by A82,METRIC_1:11;
then A84: ( ( dist_min A ) . X ) < L by A83,XXREAL_0:2;
A85: ( pk . X ) >= L by A69;
( pk . X ) = ( max (( pk1 . X ),( ( dist_min A ) . X )) ) by A81;
then P[ k1 ] by A23,A78,A84,A85,FINSEQ_3:25,XXREAL_0:16;
then k1 >= ( k1 + 1 ) by A70;
hence contradiction by NAT_1:13;
end;
end;
end;
end;
