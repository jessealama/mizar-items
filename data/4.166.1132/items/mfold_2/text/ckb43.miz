environ
vocabularies NUMBERS,SUBSET_1,XREAL_0,ORDINAL1,PRE_TOPC,EUCLID,ARYTM_1,ARYTM_3,SUPINF_2,XBOOLE_0,PRVECT_1,FUNCOP_1,XXREAL_0,RLSUB_2,ORDINAL2,METRIC_1,TARSKI,STRUCT_0,REAL_1,MATRIXR2,FINSEQ_1,FINSEQ_2,COMPTS_1,BINOP_2,TOPS_2,T_0TOPSP,CONNSP_2,FUNCT_1,RELAT_1,CARD_1,FUNCT_2,VALUED_1,WAYBEL23,RLVECT_3,FINSET_1,NAT_1,PARTFUN1,SETFAM_1,ORDINAL4,ALGSTR_0,EUCLID_7,CARD_3,RCOMP_1,TOPREALB,RVSUM_1,RLSUB_1,RLVECT_2,FUNCSDOM,RLVECT_1,RLTOPSP1,VECTSP_1,MATRIX_1,MATRLIN2,RLVECT_5,ZFMISC_1,PCOMPS_1,COMPLEX1,SQUARE_1,TOPMETR,INCSP_1,MFOLD_1,MFOLD_2;
notations TARSKI,XBOOLE_0,SUBSET_1,SETFAM_1,FUNCOP_1,FUNCT_1,TOPS_2,RELAT_1,CANTOR_1,RELSET_1,PARTFUN1,FUNCT_2,ORDINAL1,NAT_1,NUMBERS,VALUED_0,VALUED_1,XCMPLX_0,XREAL_0,XXREAL_0,COMPLEX1,REAL_1,RVSUM_1,PRE_TOPC,METRIC_1,TOPREAL9,ALGSTR_0,CONNSP_2,TSEP_1,METRIZTS,COMPTS_1,EUCLID,SQUARE_1,TOPMETR,TMAP_1,MEMBERED,XXREAL_2,TOPREAL6,WAYBEL23,CARD_1,TOPREALB,RLSUB_1,RLVECT_3,RLTOPSP1,PRVECT_1,VECTSP_1,MATRIX_1,MATRIX_6,MATRTOP1,RLVECT_2,RLVECT_5,FUNCSDOM,FINSEQ_1,FINSET_1,FINSEQ_2,EUCLID_7,STRUCT_0,RLVECT_1,FVSUM_1,RLSUB_2,CARD_3,T_0TOPSP,BORSUK_3,BINOP_2,ZFMISC_1,SEQ_1,NAT_D,PCOMPS_1,BINOP_1,RUSUB_4,CONVEX1,MONOID_0,MFOLD_1,CKB6,CKB30,CKB32,CKB36,CKB41;
definitions EUCLID,FINSEQ_1,MATRTOP1,STRUCT_0,TARSKI,VECTSP_1,XBOOLE_0,FUNCT_1,CKB30,CKB32,CKB36,CKB41;
theorems TOPREAL9,TOPREALB,METRIZTS,TSEP_1,YELLOW14,T_0TOPSP,RLVECT_5,ZFMISC_1,TARSKI,RELAT_1,FUNCT_1,RLTOPSP1,EUCLID,FUNCT_2,RLSUB_1,XBOOLE_0,EUCLID_2,JGRAPH_5,ORDINAL1,RLVECT_3,STRUCT_0,MATRTOP2,FUNCSDOM,FINSEQ_2,EUCLID_7,CARD_1,RLVECT_2,RLVECT_1,NAT_1,PARTFUN1,FINSEQ_3,MATRIX13,FINSEQ_1,RLSUB_2,RLVECT_4,XCMPLX_1,PRE_TOPC,WAYBEL23,TOPS_2,BORSUK_3,CONNSP_2,XBOOLE_1,TSP_1,SUBSET_1,INT_1,RVSUM_1,FUNCOP_1,VALUED_1,MATRIXR2,RELSET_1,XREAL_1,FINSEQ_4,PCOMPS_1,XREAL_0,JORDAN,REAL_NS1,SQUARE_1,ALGSTR_0,XXREAL_0,METRIC_1,TOPMETR,TOPREAL3,UNIFORM1,JGRAPH_1,JGRAPH_2,JGRAPH_6,TMAP_1,JORDAN2C,FRECHET,MFOLD_1,FUNCTOR0,RLAFFIN3,CKB3,CKB4,CKB5,CKB7,CKB8,CKB9,CKB10,CKB11,CKB12,CKB13,CKB14,CKB15,CKB16,CKB17,CKB18,CKB19,CKB20,CKB21,CKB22,CKB23,CKB24,CKB25,CKB26,CKB27,CKB28,CKB29,CKB30,CKB32,CKB33,CKB34,CKB35,CKB36,CKB37,CKB38,CKB39,CKB40,CKB41;
schemes NAT_1,FUNCT_2,CLASSES1;
registrations XBOOLE_0,XXREAL_0,XREAL_0,XCMPLX_0,SQUARE_1,NAT_1,MEMBERED,STRUCT_0,METRIC_1,MONOID_0,EUCLID,TOPALG_2,VALUED_0,FINSEQ_1,FUNCT_1,RELAT_1,PRE_TOPC,TOPS_1,CARD_1,TSEP_1,TOPREAL9,PCOMPS_1,RLVECT_1,COMPLEX1,TMAP_1,COMPTS_1,TOPMETR,XXREAL_2,TOPREAL6,SPPOL_1,WAYBEL23,METRIZTS,RLVECT_3,TOPREAL1,YELLOW13,YELLOW_8,KURATO_2,SUBSET_1,TOPREALB,PRVECT_1,VECTSP_1,RLVECT_2,RLVECT_5,EUCLID_7,FUNCSDOM,FINSET_1,FUNCT_2,FINSEQ_2,FUNCOP_1,FVSUM_1,RLSUB_1,REAL_1,ORDINAL1,ALGSTR_0,RLSUB_2,MATRTOP1,CANTOR_1,CARD_3,BORSUK_3,RVSUM_1,NUMBERS,RLTOPSP1,PARTFUN1,RELSET_1,GRFUNC_1,MFOLD_1,RLAFFIN3,CKB1,CKB2,CKB31,CKB42;
constructors MONOID_0,TOPREAL9,TOPREALB,SQUARE_1,FUNCSDOM,TOPS_1,TSEP_1,METRIZTS,PCOMPS_1,RLVECT_1,COMPLEX1,COMPTS_1,XCMPLX_0,NAT_1,EUCLID,BINOP_2,TOPMETR,TMAP_1,MEMBERED,XXREAL_2,TOPREAL6,WAYBEL23,RLSUB_1,RLVECT_3,RLTOPSP1,PRVECT_1,VECTSP_1,VECTSP_4,MATRIX_1,MATRIX_6,MATRTOP1,STRUCT_0,RLVECT_2,RLVECT_5,FINSEQ_1,FINSET_1,FINSEQ_2,EUCLID_7,FVSUM_1,RLSUB_2,CANTOR_1,CARD_3,BORSUK_3,SEQ_1,NAT_D,RELAT_1,RUSUB_4,CONVEX1,NUMBERS,MFOLD_1,CKB6,CKB30,CKB32,CKB36,CKB41;
requirements NUMERALS,BOOLE,SUBSET,ARITHM,REAL;
begin
reserve n for  natural number;
reserve p for (Point of ( TOP-REAL n ));
reserve q for (Point of ( TOP-REAL n ));
definition
let n;
let p;
let S being (SubSpace of ( TOP-REAL n ));
assume A1: p in ( Sphere (( 0. ( TOP-REAL n ) ),1) );
func stereographic_projection (S,p) -> (Function of S,( TPlane (p,( 0. ( TOP-REAL n ) )) )) means 
:Def4: (for q holds (q in S implies ( it . q ) = ( ( 1 / ( 1 - |( q,p )| ) ) * ( q - ( |( q,p )| * p ) ) )));
existence
proof
set T = ( TPlane (p,( 0. ( TOP-REAL n ) )) );
defpred P[ set,set ]
 means
(for q being (Point of ( TOP-REAL n )) holds ((q = $1 & q in S) implies $2 = ( ( 1 / ( 1 - |( q,p )| ) ) * ( q - ( |( q,p )| * p ) ) )));
A2: (for x being set holds (x in ( [#] S ) implies (ex y being set st (y in ( [#] T ) & P[ x,y ]))))
proof
let x being set;
assume A3: x in ( [#] S );
( [#] S ) c= ( [#] ( TOP-REAL n ) ) by PRE_TOPC:def 4;
then reconsider q = x as (Point of ( TOP-REAL n )) by A3;
set y = ( ( 1 / ( 1 - |( q,p )| ) ) * ( q - ( |( q,p )| * p ) ) );
take y;
reconsider n1 = n as (Element of ( NAT )) by ORDINAL1:def 12;
reconsider p1 = p as (Point of ( TOP-REAL n1 ));
|. ( p1 - ( 0. ( TOP-REAL n1 ) ) ) .| = 1 by A1,TOPREAL9:9;
then |. ( p1 + ( - ( 0. ( TOP-REAL n1 ) ) ) ) .| = 1 by EUCLID:41;
then |. ( p1 + ( ( - 1 ) * ( 0. ( TOP-REAL n1 ) ) ) ) .| = 1 by EUCLID:39;
then |. ( p1 + ( 0. ( TOP-REAL n1 ) ) ) .| = 1 by EUCLID:28;
then |. p .| = 1 by EUCLID:27;
then |( p,p )| = ( 1 ^2 ) by EUCLID_2:4;
then A4: |( p,p )| = ( 1 * 1 ) by SQUARE_1:def 1;
A5: |( p,( |( q,p )| * p ) )| = ( |( q,p )| * |( p,p )| ) by EUCLID_2:20
.= |( p,q )| by A4;
|( p,y )| = ( ( 1 / ( 1 - |( q,p )| ) ) * |( p,( q - ( |( q,p )| * p ) ) )| ) by EUCLID_2:20
.= ( ( 1 / ( 1 - |( q,p )| ) ) * ( |( p,q )| - |( p,( |( q,p )| * p ) )| ) ) by EUCLID_2:27
.= ( 0 ) by A5;
then |( p,( y + ( 0. ( TOP-REAL n ) ) ) )| = ( 0 ) by EUCLID:27;
then |( p,( y + ( ( - 1 ) * ( 0. ( TOP-REAL n ) ) ) ) )| = ( 0 ) by EUCLID:28;
then |( p,( y + ( - ( 0. ( TOP-REAL n ) ) ) ) )| = ( 0 ) by EUCLID:39;
then |( p,( y - ( 0. ( TOP-REAL n ) ) ) )| = ( 0 ) by EUCLID:41;
then y in ( Plane (p,( 0. ( TOP-REAL n ) )) );
hence y in ( [#] T ) by PRE_TOPC:def 5;
thus P[ x,y ];
end;
consider f being (Function of ( [#] S ),( [#] T )) such that A6: (for x being set holds (x in ( [#] S ) implies P[ x,( f . x ) ])) from FUNCT_2:sch 1(A2);
reconsider f as (Function of S,T);
take f;
let q being (Point of ( TOP-REAL n ));
assume A7: q in S;
then q in ( [#] S ) by STRUCT_0:def 5;
hence thesis by A6,A7;
end;
uniqueness
proof
let f1 being (Function of S,( TPlane (p,( 0. ( TOP-REAL n ) )) ));
let f2 being (Function of S,( TPlane (p,( 0. ( TOP-REAL n ) )) ));
assume that
A8: (for q being (Point of ( TOP-REAL n )) holds (q in S implies ( f1 . q ) = ( ( 1 / ( 1 - |( q,p )| ) ) * ( q - ( |( q,p )| * p ) ) )))
and
A9: (for q being (Point of ( TOP-REAL n )) holds (q in S implies ( f2 . q ) = ( ( 1 / ( 1 - |( q,p )| ) ) * ( q - ( |( q,p )| * p ) ) )));
(for x being set holds (x in ( [#] S ) implies ( f1 . x ) = ( f2 . x )))
proof
let x being set;
assume A10: x in ( [#] S );
( [#] S ) c= ( [#] ( TOP-REAL n ) ) by PRE_TOPC:def 4;
then reconsider q = x as (Point of ( TOP-REAL n )) by A10;
A11: q in S by A10,STRUCT_0:def 5;
thus ( f1 . x ) = ( ( 1 / ( 1 - |( q,p )| ) ) * ( q - ( |( q,p )| * p ) ) ) by A11,A8
.= ( f2 . x ) by A11,A9;
end;
hence f1 = f2 by FUNCT_2:12;
end;
end;
