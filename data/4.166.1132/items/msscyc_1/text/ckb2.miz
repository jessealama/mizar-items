environ
vocabularies NUMBERS,FINSET_1,FUNCT_1,RELAT_1,CARD_3,FUNCT_2,GRAPH_1,SUBSET_1,TREES_2,FINSEQ_1,STRUCT_0,GRAPH_2,ARYTM_3,XXREAL_0,NAT_1,PARTFUN1,TARSKI,ORDINAL4,XBOOLE_0,CARD_1,WAYBEL_0,GLIB_000,RELAT_2,FUNCOP_1,ARYTM_1,TREES_4,TREES_1,MSUALG_1,PBOOLE,MSATERM,TREES_9,ZFMISC_1,MSAFREE,MSUALG_2,MSAFREE2,MSUALG_3,PRELAMB,REALSET1,GROUP_6,FUNCT_6,MARGREL1,UNIALG_2,DTCONSTR,TDGROUP,TREES_3,TREES_A,MSSCYC_1;
notations TARSKI,XBOOLE_0,ZFMISC_1,SUBSET_1,XCMPLX_0,CARD_1,NUMBERS,NAT_1,FINSEQ_2,STRUCT_0,CARD_3,RELAT_1,FUNCT_1,PARTFUN1,FUNCT_2,FINSET_1,FUNCOP_1,FINSEQ_1,GRAPH_1,GRAPH_2,TREES_1,TREES_2,TREES_3,FUNCT_6,TREES_4,LANG1,DTCONSTR,PBOOLE,MSUALG_1,MSUALG_2,MSUALG_3,MSAFREE,MSAFREE2,TREES_9,MSATERM,XXREAL_0,PRE_POLY;
definitions TARSKI,GRAPH_1,GRAPH_2,MSAFREE,PBOOLE,FINSET_1,MSUALG_3,STRUCT_0;
theorems TARSKI,NAT_1,ZFMISC_1,FINSET_1,FRAENKEL,GRAPH_1,GRAPH_2,FUNCOP_1,FUNCT_1,FUNCT_2,FUNCT_6,FINSEQ_1,FINSEQ_2,FINSEQ_3,FINSEQ_4,FINSEQ_5,CARD_1,CARD_3,TREES_1,TREES_3,TREES_4,PBOOLE,MSUALG_1,MSUALG_2,MSUALG_3,MSAFREE,MSAFREE2,MSATERM,EXTENS_1,INT_1,XBOOLE_0,XBOOLE_1,XREAL_1,XXREAL_0,ORDINAL1,PARTFUN1,RELAT_1,CARD_2,CKB1;
schemes NAT_1,FRAENKEL,PBOOLE,DOMAIN_1;
registrations XBOOLE_0,RELAT_1,FUNCT_1,FINSET_1,XXREAL_0,XREAL_0,NAT_1,INT_1,FINSEQ_1,PBOOLE,TREES_2,TREES_3,PRE_CIRC,TREES_9,STRUCT_0,DTCONSTR,MSUALG_1,MSAFREE,MSAFREE2,EXTENS_1,ORDINAL1,CARD_1,GRAPH_1,RELSET_1,TREES_1,MSATERM;
constructors WELLORD2,XXREAL_0,NAT_1,NAT_D,FINSEQ_4,TREES_9,MSSUBFAM,MSUALG_3,MSATERM,MSAFREE2,GRAPH_2,RELSET_1,PRE_POLY,DOMAIN_1,FINSEQ_2;
requirements NUMERALS,REAL,BOOLE,SUBSET,ARITHM;
begin
reserve G for Graph;
reserve n for (Element of ( NAT ));
definition
let G being Graph;
redefine mode Chain of G
means :Def1: (it is (FinSequence of (the carrier' of G)) & (ex p being (FinSequence of (the carrier of G)) st p is_vertex_seq_of it));
compatibility
proof
let c being FinSequence;
hereby
assume A1: c is (Chain of G);
then consider p being FinSequence such that A2: ( len p ) = ( ( len c ) + 1 ) and A3: (for n holds ((1 <= n & n <= ( len p )) implies ( p . n ) in (the carrier of G))) and A4: (for n holds ((1 <= n & n <= ( len c )) implies (ex x9,y9 being (Element of (the carrier of G)) st ((x9 = ( p . n ) & y9 = ( p . ( n + 1 ) )) & ( c . n ) joins x9,y9)))) by GRAPH_1:def 12;
thus c is (FinSequence of (the carrier' of G)) by A1;
now
let i being Nat;
assume i in ( dom p );
then A5: (1 <= i & i <= ( len p )) by FINSEQ_3:25;
i in ( NAT ) by ORDINAL1:def 12;
hence ( p . i ) in (the carrier of G) by A3,A5;
end;
then reconsider p as (FinSequence of (the carrier of G)) by FINSEQ_2:12;
take p;
thus p is_vertex_seq_of c
proof
thus ( len p ) = ( ( len c ) + 1 ) by A2;
let n;
assume that
A6: 1 <= n
and
A7: n <= ( len c );
( n + 1 ) <= ( len p ) by A2,A7,XREAL_1:6;
then A8: ( p /. ( n + 1 ) ) = ( p . ( n + 1 ) ) by FINSEQ_4:15,NAT_1:12;
(n <= ( len p ) & (ex x9,y9 being (Element of (the carrier of G)) st ((x9 = ( p . n ) & y9 = ( p . ( n + 1 ) )) & ( c . n ) joins x9,y9))) by A2,A4,A6,A7,NAT_1:12;
hence thesis by A6,A8,FINSEQ_4:15;
end;

end;
assume A9: c is (FinSequence of (the carrier' of G));
given p being (FinSequence of (the carrier of G)) such that
A10: p is_vertex_seq_of c;

hereby
let n;
assume (1 <= n & n <= ( len c ));
then n in ( dom c ) by FINSEQ_3:25;
then A11: ( c . n ) in ( rng c ) by FUNCT_1:def 3;
( rng c ) c= (the carrier' of G) by A9,FINSEQ_1:def 4;
hence ( c . n ) in (the carrier' of G) by A11;
end;
take p;
thus A12: ( len p ) = ( ( len c ) + 1 ) by A10,GRAPH_2:def 6;
hereby
let n;
assume (1 <= n & n <= ( len p ));
then n in ( dom p ) by FINSEQ_3:25;
then A13: ( p . n ) in ( rng p ) by FUNCT_1:def 3;
( rng p ) c= (the carrier of G) by FINSEQ_1:def 4;
hence ( p . n ) in (the carrier of G) by A13;
end;
let n;
assume that
A14: 1 <= n
and
A15: n <= ( len c );
take x9 = ( p /. n );
take y9 = ( p /. ( n + 1 ) );
(n <= ( len p ) & ( n + 1 ) <= ( len p )) by A12,A15,NAT_1:12,XREAL_1:6;
hence (x9 = ( p . n ) & y9 = ( p . ( n + 1 ) )) by A14,FINSEQ_4:15,NAT_1:12;
thus thesis by A10,A14,A15,GRAPH_2:def 6;
end;
end;
