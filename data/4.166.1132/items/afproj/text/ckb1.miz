environ
vocabularies DIRAF,SUBSET_1,STRUCT_0,AFF_4,INCSP_1,AFF_1,ANALOAF,RELAT_1,TARSKI,PARSP_1,XBOOLE_0,ARYTM_3,SETFAM_1,ZFMISC_1,EQREL_1,RELAT_2,ANPROJ_1,INCPROJ,MCART_1,FDIFF_1,ANPROJ_2,AFF_2,VECTSP_1,AFPROJ;
notations TARSKI,XBOOLE_0,ZFMISC_1,SUBSET_1,DOMAIN_1,EQREL_1,RELSET_1,RELAT_1,RELAT_2,MCART_1,STRUCT_0,ANALOAF,DIRAF,AFF_1,AFF_4,AFF_2,PAPDESAF,INCSP_1,INCPROJ;
definitions TARSKI;
theorems RELAT_1,RELAT_2,TARSKI,ZFMISC_1,MCART_1,EQREL_1,AFF_1,AFF_4,INCPROJ,PAPDESAF,AFF_2,DIRAF,INCSP_1,XBOOLE_0,PARTFUN1,ORDERS_1;
schemes RELSET_1;
registrations XBOOLE_0,SUBSET_1,RELSET_1,STRUCT_0,INCPROJ;
constructors DOMAIN_1,EQREL_1,AFF_1,AFF_2,TRANSLAC,INCPROJ,AFF_4,RELSET_1;
requirements SUBSET,BOOLE;
begin
reserve AS for AffinSpace;
reserve X for (Subset of AS);
reserve zz for (Element of AS);
reserve x for set;
theorem
Th1: ((AS is AffinPlane & X = (the carrier of AS)) implies X is  being_plane)
proof
assume that
A1: AS is AffinPlane
and
A2: X = (the carrier of AS);
consider a,b,c being (Element of AS) such that A3: (not  LIN a,b,c) by AFF_1:12;
set P = ( Line (a,b) );
set K = ( Line (a,c) );
A4: b in P by AFF_1:15;
A5: c in K by AFF_1:15;
a <> b by A3,AFF_1:7;
then A6: P is  being_line by AFF_1:def 3;
set Y = ( Plane (K,P) );
A7: a in P by AFF_1:15;
a <> c by A3,AFF_1:7;
then A8: K is  being_line by AFF_1:def 3;
A9: a in K by AFF_1:15;
A10: (not K // P)
proof
assume K // P;
then c in P by A7,A9,A5,AFF_1:45;
hence contradiction by A3,A7,A4,A6,AFF_1:21;
end;
now
let x;
assume x in X;
then reconsider a = x as (Element of AS);
set K9 = ( a * K );
A11: K9 is  being_line by A8,AFF_4:27;
A12: K // K9 by A8,AFF_4:def 3;
then (not K9 // P) by A10,AFF_1:44;
then consider b being (Element of AS) such that A13: b in K9 and A14: b in P by A1,A6,A11,AFF_1:58;
a in K9 by A8,AFF_4:def 3;
then a,b // K by A12,A13,AFF_1:40;
then a in { zz: (ex b being (Element of AS) st (zz,b // K & b in P)) } by A14;
hence x in Y by AFF_4:def 1;
end;
then A15: X c= Y by TARSKI:def 3;
Y is  being_plane by A6,A8,A10,AFF_4:def 2;
hence thesis by A2,A15,XBOOLE_0:def 10;
end;
