environ
vocabularies NUMBERS,XREAL_0,ORDINAL1,CARD_1,XXREAL_0,ARYTM_3,RELAT_1,ARYTM_1,XBOOLE_0,SUBSET_1,FUNCT_1,MCART_1,ZFMISC_1,NORMSP_1,PRE_TOPC,METRIC_1,SUPINF_2,TARSKI,REAL_1,COMPLEX1,LOPBAN_1,STRUCT_0,NORMSP_2,RCOMP_1,CONVEX1,XXREAL_2,XCMPLX_0,PREPOWER,SERIES_1,NEWTON,NAT_1,CARD_3,ORDINAL2,SEQ_2,RSSPACE3,FUNCT_2,PARTFUN1,FCONT_1,PROB_1;
notations TARSKI,XBOOLE_0,SUBSET_1,ZFMISC_1,FUNCT_1,RELSET_1,FUNCT_2,PARTFUN1,MCART_1,PRE_TOPC,DOMAIN_1,ORDINAL1,NUMBERS,XCMPLX_0,XREAL_0,COMPLEX1,REAL_1,NAT_1,STRUCT_0,XXREAL_0,SEQ_2,NEWTON,PREPOWER,SERIES_1,RLVECT_1,CONVEX1,RUSUB_4,NORMSP_0,NORMSP_1,NORMSP_2,RSSPACE3,LOPBAN_1,NFCONT_1,LOPBAN_5,KURATO_2,T_0TOPSP;
definitions TARSKI,RLVECT_1,METRIC_1,PCOMPS_1,CONVEX1,RUSUB_4,T_0TOPSP,ALGSTR_0,NORMSP_2,XBOOLE_0,LOPBAN_1,LOPBAN_5,NFCONT_1,NORMSP_0;
theorems TARSKI,XBOOLE_1,SEQ_2,RLVECT_1,XREAL_0,RELAT_1,RUSUB_4,RLTOPSP1,SERIES_1,PREPOWER,ZFMISC_1,FUNCT_2,XBOOLE_0,XREAL_1,XCMPLX_1,NORMSP_1,PRE_TOPC,NFCONT_1,XXREAL_0,FUNCT_1,NEWTON,MESFUNC1,NORMSP_2,RINFSUP1,ABSVALUE,XCMPLX_0,LOPBAN_3,ORDINAL1,SUBSET_1,RSSPACE2,LOPBAN_1,NAT_1,PROB_1,LOPBAN_5,CONVEX1,MCART_1,GRCAT_1,NORMSP_0,CKB1,CKB3,CKB4;
schemes FUNCT_2,RECDEF_1,NAT_1,CKB2;
registrations REAL_1,XREAL_0,XXREAL_0,ORDINAL1,RELSET_1,STRUCT_0,TOPS_1,SUBSET_1,NAT_1,NORMSP_1,NORMSP_2,FUNCT_2,LOPBAN_1,NORMSP_0;
constructors XCMPLX_0,XXREAL_0,XREAL_0,ORDINAL2,TOPS_1,REAL_1,DOMAIN_1,SEQ_1,SEQ_2,PCOMPS_1,RUSUB_4,CONVEX1,RLTOPSP1,NFCONT_1,NAT_1,FUNCT_1,FUNCT_2,PSCOMP_1,PARTFUN1,NEWTON,PREPOWER,SERIES_1,NORMSP_2,RSSPACE3,LOPBAN_1,LOPBAN_5,T_0TOPSP,RELSET_1;
requirements REAL,NUMERALS,SUBSET,BOOLE,ARITHM;
begin
reserve X for RealNormSpace;
reserve Y for RealNormSpace;
theorem
(for T being (LinearOperator of X,Y) holds (for B0,B1 being (Subset of X) holds ( T .: ( B0 + B1 ) ) = ( ( T .: B0 ) + ( T .: B1 ) )))
proof
let T being (LinearOperator of X,Y);
let B0 being (Subset of X);
let B1 being (Subset of X);
thus ( T .: ( B0 + B1 ) ) c= ( ( T .: B0 ) + ( T .: B1 ) )
proof
let t being set;
assume t in ( T .: ( B0 + B1 ) );
then consider z1 being set such that z1 in (the carrier of X) and A1: z1 in ( B0 + B1 ) and A2: t = ( T . z1 ) by FUNCT_2:64;
consider x1,x2 being (Element of X) such that A3: z1 = ( x1 + x2 ) and A4: (x1 in B0 & x2 in B1) by A1;
A5: (( T . x1 ) in ( T .: B0 ) & ( T . x2 ) in ( T .: B1 )) by A4,FUNCT_2:35;
t = ( ( T . x1 ) + ( T . x2 ) ) by A2,A3,GRCAT_1:def 8;
hence thesis by A5;
end;

let t being set;
assume t in ( ( T .: B0 ) + ( T .: B1 ) );
then consider tz0,tz1 being (Point of Y) such that A6: t = ( tz0 + tz1 ) and A7: tz0 in ( T .: B0 ) and A8: tz1 in ( T .: B1 );
consider z1 being (Element of X) such that A9: z1 in B1 and A10: tz1 = ( T . z1 ) by A8,FUNCT_2:65;
consider z0 being (Element of X) such that A11: z0 in B0 and A12: tz0 = ( T . z0 ) by A7,FUNCT_2:65;
reconsider z1 as (Point of X);
reconsider z0 as (Point of X);
A13: ( z0 + z1 ) in ( B0 + B1 ) by A11,A9;
t = ( T . ( z0 + z1 ) ) by A6,A12,A10,GRCAT_1:def 8;
hence thesis by A13,FUNCT_2:35;
end;
