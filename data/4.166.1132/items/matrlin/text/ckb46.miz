environ
vocabularies NUMBERS,NAT_1,XBOOLE_0,MATRIX_1,FINSEQ_3,RELAT_1,FINSEQ_1,TARSKI,ARYTM_3,XXREAL_0,CARD_1,TREES_1,FUNCT_1,INCSP_1,ORDINAL4,SUBSET_1,VECTSP_1,RLVECT_2,RLVECT_3,CARD_3,ARYTM_1,SUPINF_2,RLVECT_5,FINSET_1,STRUCT_0,RLSUB_1,ALGSTR_0,PARTFUN1,RLVECT_1,ZFMISC_1,QC_LANG1,FUNCT_2,VALUED_1,PRE_POLY,FINSEQ_2,RVSUM_1,FINSEQ_4,FVSUM_1,MATRLIN,MSSUBFAM,UNIALG_1;
notations TARSKI,XBOOLE_0,ZFMISC_1,SUBSET_1,NUMBERS,XCMPLX_0,NAT_1,MOD_2,FINSEQ_1,RELAT_1,FUNCT_1,PARTFUN1,FUNCT_2,FUNCT_3,STRUCT_0,ALGSTR_0,MATRIX_1,MATRIX_3,FVSUM_1,RLVECT_1,VECTSP_1,VECTSP_4,VECTSP_6,VECTSP_7,FINSET_1,FINSEQ_3,FINSEQ_2,FINSEQOP,BINOP_1,XXREAL_0,PRE_POLY,GRCAT_1,CKB1,CKB6,CKB11,CKB13,CKB14,CKB15,CKB19,CKB21,CKB33,CKB34;
definitions TARSKI,FVSUM_1,FUNCOP_1,ALGSTR_0,PRE_POLY,CKB11,CKB13,CKB14,CKB15,CKB19,CKB21;
theorems TARSKI,FINSEQ_1,FINSEQ_2,FINSEQ_3,FINSEQ_4,VECTSP_1,VECTSP_4,VECTSP_6,VECTSP_7,RLVECT_1,FUNCT_1,FUNCT_2,FUNCT_3,MATRIX_1,MATRIX_2,MATRIX_3,MOD_2,ZFMISC_1,NAT_1,FVSUM_1,SUBSET_1,FINSEQOP,FUNCOP_1,RELAT_1,PARTFUN2,XBOOLE_0,XBOOLE_1,RLVECT_2,ORDINAL1,PARTFUN1,PRE_POLY,GRCAT_1,CKB2,CKB3,CKB4,CKB5,CKB7,CKB8,CKB9,CKB10,CKB11,CKB13,CKB14,CKB15,CKB16,CKB17,CKB18,CKB19,CKB20,CKB21,CKB22,CKB23,CKB24,CKB25,CKB26,CKB27,CKB28,CKB29,CKB30,CKB31,CKB35,CKB36,CKB37,CKB38,CKB39,CKB40,CKB41,CKB42,CKB43,CKB44,CKB45;
schemes FINSEQ_1,FINSEQ_2,FUNCT_2,MATRIX_1,NAT_1,FINSEQ_4;
registrations XBOOLE_0,SUBSET_1,ORDINAL1,RELSET_1,FUNCT_2,FINSET_1,XXREAL_0,XREAL_0,NAT_1,FINSEQ_1,STRUCT_0,VECTSP_1,MATRIX_1,CARD_1,VECTSP_7,MOD_2,GRCAT_1,PRE_POLY,RELAT_1,FUNCT_1,FINSEQ_4,CKB12,CKB32;
constructors PARTFUN1,BINOP_1,FUNCT_3,SQUARE_1,NAT_1,VECTSP_6,VECTSP_7,MOD_2,FVSUM_1,MATRIX_3,RELSET_1,PRE_POLY,GRCAT_1,CKB1,CKB6,CKB11,CKB13,CKB14,CKB15,CKB19,CKB21,CKB33,CKB34;
requirements REAL,NUMERALS,BOOLE,SUBSET,ARITHM;
begin
reserve k for Nat;
reserve K for Field;
reserve V for (VectSp of K);
reserve W for (Element of V);
reserve KL1 for (Linear_Combination of V);
reserve KL2 for (Linear_Combination of V);
reserve V1 for  finite-dimensional (VectSp of K);
reserve b1 for (OrdBasis of V1);
reserve KL for (Linear_Combination of V1);
definition
let K being Field;
let V being  finite-dimensional (VectSp of K);
let b1 being (OrdBasis of V);
let W being (Element of V);
func W |-- b1 -> (FinSequence of K) means 
:Def9: (( len it ) = ( len b1 ) & (ex KL being (Linear_Combination of V) st ((W = ( Sum KL ) & ( Carrier KL ) c= ( rng b1 )) & (for k holds ((1 <= k & k <= ( len it )) implies ( it /. k ) = ( KL . ( b1 /. k ) ))))));
existence
proof
reconsider b2 = ( rng b1 ) as (Basis of V) by CKB13:def 1;
consider KL being (Linear_Combination of V) such that A1: W = ( Sum KL ) and A2: ( Carrier KL ) c= b2 by CKB10:1;
deffunc V(Nat) = ( KL . ( b1 /. $1 ) );
consider A being (FinSequence of K) such that A3: ( len A ) = ( len b1 ) and A4: (for k being Nat holds (k in ( dom A ) implies ( A . k ) = V(k))) from FINSEQ_2:sch 1;
take A;
thus ( len A ) = ( len b1 ) by A3;
take KL;
thus W = ( Sum KL ) by A1;
thus ( Carrier KL ) c= ( rng b1 ) by A2;
let k;
A5: ( dom A ) = ( Seg ( len b1 ) ) by A3,FINSEQ_1:def 3;
assume (1 <= k & k <= ( len A ));
then A6: k in ( Seg ( len b1 ) ) by A3,FINSEQ_1:1;
then k in ( dom A ) by A3,FINSEQ_1:def 3;
then ( A . k ) = ( A /. k ) by PARTFUN1:def 6;
hence thesis by A4,A5,A6;
end;
uniqueness
proof
reconsider b = ( rng b1 ) as (Basis of V) by CKB13:def 1;
let F1 being (FinSequence of K);
let F2 being (FinSequence of K);
assume A7: ( len F1 ) = ( len b1 );
given KL1 being (Linear_Combination of V) such that
A8: (W = ( Sum KL1 ) & ( Carrier KL1 ) c= ( rng b1 ))
and
A9: (for k holds ((1 <= k & k <= ( len F1 )) implies ( F1 /. k ) = ( KL1 . ( b1 /. k ) )));

assume A10: ( len F2 ) = ( len b1 );
given KL2 being (Linear_Combination of V) such that
A11: (W = ( Sum KL2 ) & ( Carrier KL2 ) c= ( rng b1 ))
and
A12: (for k holds ((1 <= k & k <= ( len F2 )) implies ( F2 /. k ) = ( KL2 . ( b1 /. k ) )));

A13: b is  linearly-independent by VECTSP_7:def 3;
(for k being Nat holds ((1 <= k & k <= ( len F1 )) implies ( F1 . k ) = ( F2 . k )))
proof
let k being Nat;
assume A14: (1 <= k & k <= ( len F1 ));
then A15: k in ( dom F2 ) by A7,A10,FINSEQ_3:25;
k in ( dom F1 ) by A14,FINSEQ_3:25;
hence ( F1 . k ) = ( F1 /. k ) by PARTFUN1:def 6
.= ( KL1 . ( b1 /. k ) ) by A9,A14
.= ( KL2 . ( b1 /. k ) ) by A8,A11,A13,CKB7:1
.= ( F2 /. k ) by A7,A10,A12,A14
.= ( F2 . k ) by A15,PARTFUN1:def 6;
end;
hence thesis by A7,A10,FINSEQ_1:14;
end;
end;
