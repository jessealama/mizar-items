environ
vocabularies XREAL_0,ORDINAL1,ARYTM_2,SUBSET_1,ARYTM_3,ZFMISC_1,CARD_1,XXREAL_0,NUMBERS,ARYTM_0,FUNCOP_1,XBOOLE_0,TARSKI;
notations TARSKI,XBOOLE_0,ZFMISC_1,SUBSET_1,ORDINAL1,FUNCT_4,ARYTM_2,NUMBERS,ARYTM_0,XCMPLX_0,XREAL_0,XXREAL_0;
definitions TARSKI,XBOOLE_0,XXREAL_0,ARYTM_3;
theorems XBOOLE_0,ARYTM_0,ZFMISC_1,TARSKI,ARYTM_1,ARYTM_2,SUBSET_1,ORDINAL1,XBOOLE_1,XREAL_0,XCMPLX_0,NUMBERS,XXREAL_0,CKB1,CKB2,CKB3,CKB4,CKB5,CKB6,CKB7;
registrations ORDINAL1,ARYTM_2,NUMBERS,XREAL_0;
constructors FUNCT_4,ARYTM_1,ARYTM_0,XCMPLX_0,XXREAL_0,XREAL_0;
requirements SUBSET,BOOLE,NUMERALS,ARITHM;
begin
reserve r for  real number;
reserve s for  real number;
reserve k for  natural number;
reserve i for (Element of ( NAT ));
theorem
k = { i: i < k }
proof
A1: k in ( NAT ) by ORDINAL1:def 12;
thus k c= { i: i < k }
proof
reconsider k9 = k as (Element of ( NAT )) by ORDINAL1:def 12;
let e being set;
assume A2: e in k;
A3: k9 c= ( NAT ) by ORDINAL1:def 2;
then reconsider j = e as (Element of ( NAT )) by A2;
e in ( NAT ) by A3,A2;
then reconsider y9 = e as (Element of ( REAL+ )) by ARYTM_2:2;
k9 in ( NAT );
then reconsider x9 = k9 as (Element of ( REAL+ )) by ARYTM_2:2;
y9 <=' x9 by A2,ARYTM_2:18;
then A4: j <= k by CKB1:1;
y9 <> x9 by A2;
then j < k by A4,XXREAL_0:1;
hence thesis;
end;

let e being set;
assume e in { i: i < k };
then consider i being (Element of ( NAT )) such that A5: e = i and A6: (not k <= i);
i in ( NAT );
then reconsider x9 = e,y9 = k as (Element of ( REAL+ )) by A5,A1,ARYTM_2:2;
(not y9 <=' x9) by A5,A6,CKB1:1;
hence thesis by A5,A6,A1,ARYTM_2:18;
end;
