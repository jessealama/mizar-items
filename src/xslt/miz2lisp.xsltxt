stylesheet "1.0";
import "utils/strings.xsl";
import "utils/list.xsl";
output method="text";

$quote-string = { "\""; }
$slash-string = { `concat ("", '\\')`; }

tpl escape (#s) {
  choose {
    when [$s = ""] {
      "";
    }
    when [contains ($s, $quote-string)] {
      $before-quote = `substring-before ($s, $quote-string)`;
      if [contains ($s, $slash-string)] {
        $before-slash = `substring-before ($s, $slash-string)`;
        $len-before-quote = `string-length ($before-quote)`;
        $len-before-slash = `string-length ($before-slash)`;
        if [$len-before-quote < $len-before-slash] {
          $after-quote = `substring-after ($s, $quote-string)`;
          $after-quote-escaped = escape (#s = $after-quote);
          `concat ($before-quote, $after-quote-escaped)`;
        } else {
          $after-slash = `substring-after ($s, $slash-string)`;
          $after-slash-escaped = escape (#s = $after-slash);
          `concat ($before-slash, $slash-string, $slash-string, $after-slash-escaped)`;
        }
      } else {
        $after-quote = `substring-after ($s, $quote-string)`;
        $after-quote-escaped = escape (#s = $after-quote);
        `concat ($before-quote, $after-quote-escaped)`;
      }
    }
    when [contains ($s, $slash-string)] {
      $before-slash = `substring-before ($s, $slash-string)`;
      $after-slash = `substring-after ($s, $slash-string)`;
      $after-slash-escaped = escape (#s = $after-slash);
      `concat ($before-slash, $slash-string, $slash-string, $after-slash-escaped)`;
    }
    otherwise {
      $s;
    }
  }
}

tpl quote (#s) {
  $escaped = escape (#s = $s);
  `concat ($quote-string, $escaped, $quote-string)`;
}

tpl [@*] {
  $attr = `name (.)`;
  $val = `string (.)`;
  " "; ":"; $attr; " "; "\""; $val; "\"";
}

tpl [@line] {
  // ignore
}

tpl [@col] {
  // ignore
}

tpl [@articleext] {
  // ignore
}

tpl [*] {
  $n = lc (#s = `name (.)`);
  "("; $n;
  for-each [@*] {
    apply [.];
  }
  if [*] {
    "\n";
    list (#separ = " ", #elems = `*`);
  }
  ")"; "\n";
}

tpl [Text-Proper] {

  "(text-proper ("; quote (#s = `@articleid`); ")"; "\n";
  list (#separ = "\n", #elems = `*`);
  ")"; "\n";
}

tpl [/] {
  if [Text-Proper] {
    apply [Text-Proper];
  } else {
    msg terminate="yes" "Error: the Text-Proper document element is missing.";
  }
}

tpl [Variables] {
  "("; list (#separ = " ", #elems = `*`); ")";
}

tpl [Item[@kind]] {
  $n = lc (#s = `@kind`);
  "("; $n;
  for-each [@*] {
    if [name(.) = "kind"] {
      // ignore
    } else {
      apply [.];
    }
  }
  if [*] {
    "\n";
    apply [*];
  }
  ")"; "\n";
}

tpl [Block[@kind = "Proof"]] {
  "(proof"; "\n"; list (#separ = "\n", #elems = `*`); ")"; "\n";
}

tpl [Item[@kind = "Case-Block"]] {
  "(case-block ";
  for-each [Block[@kind = "Case"]] {
    list (#separ = "\n", #elems = `*`);
  }
  ")";
}

tpl [Collective-Assumption] {
  for-each [Conditions] {
    "(collective-assumption "; list (#separ = "\n", #elems = `*`); ")";
  }
}

tpl [Block[@kind = "Suppose"]] {
  "(suppose-block ";
  for-each [Item[@kind = "Suppose-Head"]] {
    choose {
      when [Single-Assumption] {
        for-each [Single-Assumption] {
          "("; apply [Proposition]; ")";
        }
      }
      when [Collective-Assumption] {
        apply [Collective-Assumption];
      }
      otherwise {
        msg terminate="yes" "Don't know how to handle a Suppose-Block item.";
      }
    }
    " "; list (#separ = "\n", #elems = `following-sibling::*`);
  }
  ")";
}

tpl [Item[@kind = "Scheme-Block-Item"]] {
  apply [Block[@kind = "Scheme-Block"]];
}

tpl [Block[@kind = "Scheme-Block"]] {
  for-each [Item[@kind = "Scheme-Head"]] {
    "(scheme ";
    apply [Scheme];
    " ";
    for-each [Schematic-Variables] {
      apply [.];
      " "; apply [following-sibling::*[1]];
    }
    if [Provisional-Formulas] {
      apply [Provisional-Formulas];
    } else {
      "()";
    }
  }
  " ";
  "(proof "; "\n"; list (#separ = "\n", #elems = `following-sibling::*`); ")";
  ")";
}

tpl [Provisional-Formulas] {
  "("; list (#separ = "\n", #elems = `*`); ")";
}

tpl [Scheme] {
  quote (#s = `@spelling`);
}

tpl [Schematic-Variables] {
  "("; list (#separ = " ", #elems = `*`); ")";
}

tpl [Functor-Segment] {
  "(functor-segment "; apply [Variables]; " "; apply [Type-List]; " "; apply [Type-Specification]; ")";
}

tpl [Type-Specification] {
  apply [*[1]];
}

tpl [Type-List] {
  "("; list (#separ = " ", #elems = `*`); ")";
}

tpl [Placeholder-Term] {
  quote (#s = `@spelling`);
}

tpl [Arguments] {
  "("; apply [*]; ")";
}

tpl [Simple-Term] {
  quote (#s = `@spelling`);
}

tpl [Variable] {
  quote (#s = `@spelling`);
}

tpl [Predicative-Formula] {
  "(predicative-formula "; quote (#s = `@spelling`); if [count(*) > 0] { " "; list (#separ = " ", #elems = `*`); } ")";
}

tpl [Infix-Term] {
  "(infix-term "; quote (#s = `@spelling`); " "; apply [*[1]]; " "; apply [*[2]]; ")";
}

tpl [Circumfix-Term] {
  "(circumfix-term "; quote (#s = `@spelling`); " "; list (#separ = " ", #elems = `*`); ")";
}

tpl [Right-Circumflex-Symbol] {
  quote (#s = `@spelling`);
}

tpl [Private-Predicate-Formula] {
  "(private-predicate-formula "; quote (#s = `@spelling`); if [count(*) > 0] { " "; list (#separ = " ", #elems = `*`); } ")";
}

tpl [Private-Functor-Term] {
  "(private-functor-term "; quote (#s = `@spelling`); if [*] { " "; list (#separ = " ", #elems = `*`); } ")";
}

tpl [Proposition[Label]] {
  "(proposition "; apply [Label]; " "; apply [*[not(self::Label)]]; ")";
}

tpl [Proposition[not(Label)]] {
  "(proposition () "; apply [*]; ")";
}

tpl [Label] {
  quote (#s = `@spelling`);
}

tpl [Conjunctive-Formula] {
  "(conjunctive-formula "; apply [*[1]]; " "; apply [*[2]]; ")";
}

tpl [Disjunctive-Formula] {
  "(disjunctive-formula "; apply [*[1]]; " "; apply [*[2]]; ")";
}

tpl [Biconditional-Formula] {
  "(biconditional-formula "; apply [*[1]]; " "; apply [*[2]]; ")";
}

tpl [Conditional-Formula] {
  "(conditional-formula "; apply [*[1]]; " "; apply [*[2]]; ")";
}

tpl [Negated-Formula] {
  "(negated-formula "; apply [*[1]]; ")";
}

tpl [Local-Reference] {
  quote (#s = `@spelling`);
}

tpl [Definition-Reference] {
  "(definition-reference "; quote (#s = `@spelling`); " "; `@nr`; ")";
}

tpl [Theorem-Reference] {
  "(theorem-reference "; quote (#s = `@spelling`); " "; `@nr`; ")";
}

tpl [Thesis] {
  "(thesis)";
}

tpl [Item[@kind = "Reservation"]] {
  "(reserve "; apply [Variables]; " "; apply [*[position() = last()]]; ")";
}

tpl [Item[@kind = "Property"]] {
  "("; `@property`; if [*] { " "; list (#separ = " ", #elems = `*`); } ")";
}

tpl [Item[@kind = "Correctness-Condition"]] {
  "("; `@condition`; if [*] { " "; list (#separ = " ", #elems = `*`); } ")";
}

tpl [Item[@kind = "Correctness"]] {
  $justification = `*[position() = last()]`;
  for-each [Conditions] {
    for-each [Correctness] {
      "("; `@condition`; " "; apply [$justification]; ")";
    }
  }
}

tpl [Global-Choice-Term] {
  "(the "; list (#separ = " ", #elems = `*`); ")";
}

tpl [Standard-Type] {
  "(standard-type "; quote (#s = `@spelling`); if [*] { " "; list (#separ = " ", #elems = `*`); } ")";
}

tpl variable-segments (#segments) {
  if [$segments] {
    $segment = `$segments[1]`;
    $more-segments = `$segments[position() > 1]`;
    for-each [$segment] {
      " ";
      choose {
        when [self::Implicitly-Qualified-Segment] {
          list (#separ = " ", #elems = `Variable`);
        }
        when [self::Explicitly-Qualified-Segment] {
          $type = `*[position() = last()]`;
          for-each [Variables] {
            for-each [*] {
              "("; apply [.]; " "; apply [$type]; ")";
            }
            if [position() < last()] {
              " ";
            }
          }
        }
      }
    }
  }
}

tpl [Existential-Quantifier-Formula] {
  "(existential-quantifier-formula "; "("; variable-segments (#segments = `*[position() < last()]`); ") "; apply [*[position() = last()]]; ")";
}

tpl [Universal-Quantifier-Formula] {
  "(universal-quantifier-formula "; "("; variable-segments (#segments = `*[position() < last()]`); ") "; apply [*[position() = last()]]; ")";
}

tpl [Item[@kind = "Regular-Statement" and @shape = "Compact-Statement"]] {
  "(regular-statement "; apply [*[1]]; " "; apply [*[2]]; ")";
}

tpl [Item[@kind = "Regular-Statement" and @shape = "Diffuse-Statement"]] {
  "(diffuse-statement "; list (#separ = "\n", #elems = `Block[@kind = "Now-Reasoning"]/*`); ")";
}

tpl [Item[@kind = "Choice-Statement"]] {
  "(consider "; "("; variable-segments (#segments = `Implicitly-Qualified-Segment | Explicitly-Qualified-Segment`); ") "; "("; list (#separ = " ", #elems = `Conditions/*`); ") "; apply [*[position() = last()]]; ")";
}

tpl [Item[@kind = "Existential-Assumption"]] {
  "(existential-assumption "; "("; variable-segments (#segments = `Implicitly-Qualified-Segment | Explicitly-Qualified-Segment`); ") "; "("; list (#separ = " ", #elems = `Conditions/*`); ") "; ")";
}

tpl [Scheme-Justification[@nr = "0"]] {
  "(internal-scheme-justification "; quote (#s = `@spelling`); if [*] { list (#separ = " ", #elems = `*`); } ")";
}

tpl [Scheme-Justification[not(@nr = "0")]] {
  "(external-scheme-justification "; quote (#s = `@spelling`); " "; `@nr`; " "; if [*] { list (#separ = " ", #elems = `*`); } ")";
}

tpl [Item[@kind = "Exemplification"]] {
  "(take "; list (#separ = " ", #elems = `*`); ")";
}

tpl [Item[@kind = "Generalization"]] {
  "(generalization "; variable-segments (#segments = `Implicitly-Qualified-Segment | Explicitly-Qualified-Segment`); ")";
}

tpl [Item[@kind = "Loci-Declaration"]] {
  "(loci-declaration "; variable-segments (#segments = `Implicitly-Qualified-Segment | Explicitly-Qualified-Segment`); ")";
}

tpl [Item[@kind = "Conclusion" and @shape = "Compact-Statement"]] {
  "(thus "; apply [*[1]]; " "; apply [*[2]]; ")";
}

tpl [Item[@kind = "Conclusion" and @shape = "Diffuse-Statement"]] {
  "(thus-diffuse "; apply [*[1]]; ")";
}

tpl [Item[@kind = "Conclusion" and @shape = "Iterative-Equality"]] {
  "(thus (iterative-equality ";
  for-each [Proposition] {
    apply [.];
    " "; apply [following-sibling::*[1]];
  }
  for-each [Iterative-Step] {
    " "; apply [*[1]]; " "; apply [*[2]];
  }
  "))";
}

tpl [Item[@kind = "Definition-Item"]] {
  apply [Block[@kind = "Definitional-Block"]];
}

tpl [Block[@kind = "Definitional-Block"]] {
  "(definitional-block "; list (#separ = " ", #elems = `*`); ")";
}

tpl [Block[@kind = "Hereby-Reasoning"]] {
  "(hereby "; list (#separ = "\n", #elems = `*`); ")";
}

tpl [Item[@kind = "Attribute-Definition"]] {
  "(attribute-definition "; apply [Attribute-Pattern]; " "; apply [Definiens/Label]; " "; apply [Definiens/*[position() = last()]]; ")";
}

tpl [Attribute-Pattern] {
  quote (#s = `@spelling`);
}

tpl [Straightforward-Justification] {
  "(straightforward-justification ";
  if [Link] {
    "t";
  } else {
    "()";
  }
  if [*] {
    " "; list (#elems = `*`, #separ = " ");
  }
  ")";
}

tpl [Link] {
  ""; // links are accounted for elsewhere
}

tpl [Item[@kind = "Functor-Definition"]] {
  "(functor-definition ";
  apply [Operation-Functor-Pattern | Bracket-Functor-Pattern];
  " ";
  for-each [Definiens] {
    if [Label] {
      apply [Label];
    } else {
      "()";
    }
    " ";
    apply [.];
  }
  ")";
}

tpl [Item[@kind = "Predicate-Definition"]] {
  "(predicate-definition ";
  apply [Predicate-Pattern];
  " ";
  for-each [Definiens] {
    if [Label] {
      apply [Label];
    } else {
      "()";
    }
    " ";
    apply [.];
  }
  ")";
}

tpl [Item[@kind = "Mode-Definition" and Expandable-Mode]] {
  "(expandable-mode-definition ";
  apply [Mode-Pattern];
  " ";
  for-each [Expandable-Mode] {
    apply [*[1]];
  }
  ")";
}

tpl [Item[@kind = "Mode-Definition" and Standard-Mode]] {
  "(standard-mode-definition ";
  apply [Mode-Pattern];
  " ";
  for-each [Standard-Mode] {
    for-each [Definiens] {
      if [Label] {
        apply [Label];
      } else {
        "()";
      }
      " ";
      apply [*[position() = last()]];
    }
  }
  ")";
}

tpl [Mode-Pattern] {
  "(mode-pattern "; quote (#s = `@spelling`); " "; list (#separ = " ", #elems = `Loci/*`); ")";
}

tpl [Operation-Functor-Pattern] {
  "(operation-functor-pattern "; quote (#s = `@spelling`); " "; "("; list (#separ = " ", #elems = `*[1]/*`); ")"; " "; "("; list (#separ = " ", #elems = `*[2]/*`); ")"; ")";
}

tpl [Bracket-Functor-Pattern] {
  "(bracket-functor-pattern "; quote (#s = `@spelling`); " "; apply [Right-Circumflex-Symbol]; " "; list (#separ = " ", #elems = `Loci/*`); ")";
}

tpl [Predicate-Pattern] {
  "(predicate-pattern "; quote (#s = `@spelling`); " "; "("; list (#separ = " ", #elems = `Loci[1]/*`); ") "; "("; list (#separ = " ", #elems = `Loci[2]/*`); ")"; ")";
}

tpl [Definiens] {
  apply [*[position() = last()]];
}

tpl [Adjective-Cluster] {
  "(adjective-cluster "; list (#separ = " ", #elems = `*`); ")";
}

tpl [Adjective] {
  "(adjective "; quote (#s = `@spelling`); ")";
}

tpl [NegatedAdjective] {
  "(negated-adjective "; quote (#s = `Adjective/@spelling`); ")";
}

tpl [Locus] {
  quote (#s = `@spelling`);
}

tpl [it-Term] {
  "(it)";
}
